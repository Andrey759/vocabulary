plugins {
	id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.0'
}

group = 'Andrey759'
version = '0.0.1'
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // spring boot
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.postgresql:postgresql:42.6.0'
    implementation 'org.apache.httpcomponents.client5:httpclient5:5.2.1'
    implementation 'com.theokanning.openai-gpt3-java:service:0.12.0'
    implementation 'org.telegram:telegrambots-spring-boot-starter:6.8.0'
    implementation 'org.telegram:telegrambots-abilities:6.8.0'

    // annotation processors
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
}

bootJar {
    enabled = true
}

jar {
    enabled = false
}

test {
    useJUnitPlatform()
}

task stop {
    group 'publishing'
    doLast {
        exec {
            commandLine 'ssh', 'root@185.201.28.190', "systemctl stop vocabulary"
        }
    }
}

task start {
    group 'publishing'
    doLast {
        exec {
            commandLine 'ssh', 'root@185.201.28.190', "systemctl start vocabulary"
        }
    }
}

task deploy {
    group 'publishing'
    dependsOn 'build'
    doLast {
        exec {
            commandLine 'ssh', 'root@185.201.28.190', "systemctl stop vocabulary"
        }
        exec {
            commandLine 'scp', file('build/libs/vocabulary-0.0.1.jar'), "root@185.201.28.190:/home"
        }
        exec {
            commandLine 'ssh', 'root@185.201.28.190', "systemctl start vocabulary"
        }
    }
}

task logs {
    group 'publishing'
    doLast {
        exec {
            commandLine 'scp', 'root@185.201.28.190:/home/logs/app.log', 'prod/'
        }
    }
}
